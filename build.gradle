// Hi :)

import java.util.Properties

plugins {
    id 'net.fabricmc.fabric-loom-remap' version '1.15.0-alpha.6'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.8.10'
    id 'io.github.themrmilchmann.curseforge-publish' version '0.8.0'
}

group = project.maven_group
version = project.mod_version

base {
    archivesName = project.archives_base_name + "+1.21.11"
}

tasks.named('jar', Jar) {
    archiveFileName = project.archives_base_name + "-" + project.mod_version + "+1.21.11.jar"
}

tasks.named('remapJar') {
    archiveFileName = project.archives_base_name + "-" + project.mod_version + "+1.21.11.jar"
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        url = 'https://maven.shedaniel.me/'
    }
    maven {
        url = 'https://api.modrinth.com/maven'
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url = 'https://maven.terraformersmc.com/releases/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.21.11"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.18.1"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.141.3+1.21.11"
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:11.0.99"
    modApi "com.terraformersmc:modmenu:17.0.0-beta.2"
}

loom {
    runs {
        client {
            client()
            ideConfigGenerated = true
            source = sourceSets.main
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}


def bumpVersion(String part) {
    def propsFile = file("gradle.properties")
    def props = new Properties()
    propsFile.withInputStream { props.load(it) }

    def v = props.getProperty("mod_version") ?: "0.0.0"
    def pieces = v.tokenize(".")*.toInteger()
    while (pieces.size() < 3) pieces << 0

    int major = pieces[0], minor = pieces[1], patch = pieces[2]

    switch (part) {
        case "major":
            major++; minor = 0; patch = 0; break
        case "minor":
            minor++; patch = 0; break
        case "patch":
            patch++; break
        default:
            throw new IllegalArgumentException("Unknown bump part: " + part)
    }

    def newV = "${major}.${minor}.${patch}"
    props.setProperty("mod_version", newV)

    propsFile.withOutputStream { os ->
        props.store(os, "Updated by Gradle bump task")
    }

    println("Bumped mod_version: ${v} -> ${newV}")
}

tasks.register("bumpPatch") { doLast { bumpVersion("patch") } }
tasks.register("bumpMinor") { doLast { bumpVersion("minor") } }
tasks.register("bumpMajor") { doLast { bumpVersion("major") } }

// Extract only the current version's changelog section
def getCurrentChangelog() {
    def changelog = file("CHANGELOG.md")
    if (!changelog.exists()) return ""
    
    def lines = changelog.readLines()
    def current = []
    def started = false
    
    for (line in lines) {
        if (line.startsWith("## [") && started) break
        if (line.startsWith("## [")) started = true
        if (started) current.add(line)
    }
    
    return current.join("\n")
}

// Task to write current changelog to file for GitHub Release
tasks.register("writeCurrentChangelog") {
    doLast {
        def changelogContent = getCurrentChangelog()
        def outputFile = file("build/current-changelog.md")
        outputFile.parentFile.mkdirs()
        outputFile.text = changelogContent
        println("Current changelog written to ${outputFile.path}")
    }
}

// ===== Modrinth Publishing (Minotaur) =====
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "GzPkXb2a"
    versionNumber = project.version.toString()
    versionType = "release"  // or "beta" / "alpha"
    uploadFile = tasks.remapJar
    gameVersions = ["1.21.11"]  // Adjust to your supported versions
    loaders = ["fabric"]
    changelog = getCurrentChangelog()
    syncBodyFrom = file("README.md").text  // Sync README to Modrinth description
}

// ===== CurseForge Publishing =====
curseforge {
    apiToken = System.getenv("CURSEFORGE_API_TOKEN")

    publications {
        register("curseforge") {
            projectId = "1463555"

            artifacts.register("main") {
                from(tasks.named("remapJar"))
                displayName = "CopyCoords ${project.version}"
                releaseType = "release"
                gameVersions = ["1.21.11"]
                loaders = ["fabric"]

                changelog {
                    format = "markdown"
                    content = getCurrentChangelog()
                }
            }
        }
    }
}